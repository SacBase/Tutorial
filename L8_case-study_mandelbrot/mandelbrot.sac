#define XRES 6
#define YRES 4
#define EXPAND 64

#define DEPTH 1024

#ifndef TIER
#define TIER 3
#endif

use Structures: all;
use SDLdisplay: all;
use StdIO : all;

use Fractal_tier1 : all;
use Fractal_tier2 : all;
use Stencil_tier3 : all;

inline
color[.,.] stretchRgb( color[.,.] pic, int stretch)
{
  res = with {
          (. <= iv <= .): pic[ iv / stretch];
        } : genarray( shape(pic) * stretch, black());
  return( res);
}

/* Main ******************************************************************** */

int main()
{
  disp = initMultiDisplay( [2,2], EXPAND * [YRES,XRES] );

  cmin = [toc( -2.2, 1.0)];
  cmax = [toc( 0.8, -1.0)];

  while( true) {
    expand = EXPAND;
    cur_shape = [YRES,XRES];

    do {
      plane = genComplexArray( cur_shape, cmin[[0]], cmax[[0]]);

      ts = escapeTime( plane, DEPTH);
  
      rgbs = intArrayToMonochrome( ts);
  
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [0,0]);
  
#if TIER >= 2
      ts, vs = escapeTimeAndValue( plane, DEPTH);
  
      nvs = normalizedIterationCount( ts, vs);
      rgbs = doubleArrayToRGB( nvs);
    
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [0,1]);
  
#if TIER >= 3
  
      rgbs = gaussBlur( gaussBlur25( rgbs));
  
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [1,0]);
  
      rgbs = sobelEdges( rgbs);
  
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [1,1]);
#endif /* TIER >= 3 */
#endif /* TIER >= 2 */
  
      expand = expand / 2;
      cur_shape = 2 * cur_shape;
    } while ( expand >= 1);

    q, zoom_coords = getSelectionMulti( disp, [2,2]);
    if (all( zoom_coords >= 0)) {
      cmin = [plane[ zoom_coords[[0]]]] ++ cmin;
      cmax = [plane[ zoom_coords[[1]]]] ++ cmax;
    } else {
      cmin = all( shape(cmin) > 1) ? drop( [1], cmin) : cmin;
      cmax = all( shape(cmax) > 1) ? drop( [1], cmax) : cmax;
    }
  }

  destroyDisplay( disp);

  return(0);
}

