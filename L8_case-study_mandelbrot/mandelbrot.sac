#define XRES 6
#define YRES 4
#define EXPAND 64

#define DEPTH 1024

#ifndef TIER
#define TIER 3
#endif

use Structures: all;
use SDLdisplay: all;
use StdIO : all;

use Fractal_tier1 : all;
use Fractal_tier2 : all;
use Stencil_tier3 : all;

inline
color[.,.] stretchRgb( color[.,.] pic, int stretch)
{
  res = with {
          (. <= iv <= .): pic[ iv / stretch];
        } : genarray( shape(pic) * stretch, black());
  return( res);
}

/* Main ******************************************************************** */

int main()
{
  expand = EXPAND;
  max_shape = [YRES,XRES] * expand;

  disp = initMultiDisplay( [2,2], max_shape);

  cmin = toc( -2.2, 1.0);
  cmax = toc( 0.8, -1.0);
  

  while( true) {
    plane = genComplexArray( [YRES,XRES], cmin, cmax);

    while( expand >= 1) {
      ts = escapeTime( plane, DEPTH);
  
      rgbs = intArrayToMonochrome( ts);
  
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [0,0]);
  
#if TIER >= 2
      ts, vs = escapeTimeAndValue( plane, DEPTH);
  
      nvs = normalizedIterationCount( ts, vs);
      rgbs = doubleArrayToRGB( nvs);
    
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [0,1]);
  
#if TIER >= 3
  
      rgbs = gaussBlur( gaussBlur25( rgbs));
  
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [1,0]);
  
      rgbs = sobelEdges( rgbs);
  
      drawArrayMulti( disp, stretchRgb( rgbs, expand), [1,1]);
#endif /* TIER >= 3 */
#endif /* TIER >= 2 */
  
      expand = expand / 2;
      plane = genComplexArray( 2*shape(plane), cmin, cmax);
    }

    q, zoom_coords = getSelectionMulti( disp, [2,2]);
    cmin = plane[ reverse( 2 * zoom_coords[[0]])];
    cmax = plane[ reverse( 2 * zoom_coords[[1]])];
    expand = EXPAND;
  }

  destroyDisplay( disp);

  return(0);
}

